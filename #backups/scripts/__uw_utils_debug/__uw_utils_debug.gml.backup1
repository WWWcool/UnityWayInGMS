// 2021-03-27 22:01:52
// Unity way library. For more information see the documentation here:
// https://github.com/WWWcool/UnityWayInGMS/wiki

/// Struct for encapsulate debug utils functions
/// @param {string} [_prefix] start of each debug out
/// @returns {UWUtilsDebug} created debug utils

function UWUtilsDebug() constructor
{
    prefix = argument[0] == undefined ? "" : argument[0] + " ";
    double_indent = false;
    
    /// @param {bool} _defined
    /// @param {string} _msg
    /// @param {array} [_args]
    
    static PrintlnIfDefined = function(_defined, _msg)
    {
        if(_defined)
            Println(_msg, argument_count > 2 ? argument[2] : []);
    }
    
    /// @param {string} _msg
    /// @param {array} [_args]
    
    static Println = function(_msg)
    {
        PrintlnWithIndent(_msg, 0, argument_count > 1 ? argument[1] : []);
    }
    
    /// @param {bool} _defined
    /// @param {string} _msg
    /// @param {number} _indent
    /// @param {array} [_args]
    
    static PrintlnWithIndentIfDefined = function(_defined, _msg, _indent, _args)
    {
        if(_defined)
            PrintlnWithIndent(_msg, _indent, argument_count > 3 ? argument[3] : []);
    }
    
    /// @param {string} _msg
    /// @param {number} _indent
    /// @param {array} [_args]
    
    static PrintlnWithIndent = function(_msg, _indent, _args)
    {
        show_debug_message(prefix + GetIndentString(double_indent ? 2 * _indent : _indent) + _msg);
    }
    
    /// @param {number} [_indent]
    /// @returns {string} string with passed number of indent
    
    static GetIndentString = function(_indent)
	{
		var res = "";
		repeat(_indent)
		{
			res += "\t";
		}
		return res;
	}
}

function UWUtilsError() constructor
{
	type = "unknown";
	text = "";
	count_tab = 1;
	
	static _prefix = "[UnityWay Error]";
	static _symbol_control = (function() {
		var map = {};
		map[$ "\n"] = "\\n";
		map[$ "\t"] = "\\t";
		map[$ "\\"] = "\\\\";
		return map;
	}());
	static _tab_size = 4;
	
	static SetType = function(_component) 
	{
		if is_string(_component) 
		{
			type = _component;
			exit;
		}
		if is_struct(_component) {
			type = instanceof(_component);
			exit;
		}
		type = "unknown";
	}
	
	static AddText = function() 
	{
		var _text = string_repeat(" ", _tab_size * _tab_size);
		var _i = -1;
		while (++_i < argument_count) 
			_text += FragmentText(argument[_i]);
		
		text += _text + "\n";
	}
	
	static TabLeft = function() 
	{
		count_tab = max(1, count_tab - 1);
	}
	
	static TabRight = function() 
	{
		count_tab += 1;
	}
	
	static Show = function()
	{
		show_error(Render(), true);
	}
	
	static Render = function()
	{
		return 
			(
				" " + _prefix + " : " + type + "\n" +
				string_repeat("*", 10) +
				text
			);
	}
	
	static FragmentText = function(_text)
	{
		return FragmentTextSymbol(_text);
	}
	
	static FragmentTextSymbol = function(_text)
	{
		if(!is_string(_text))
			_text = string(_text);
		
		var _length = string_length(_text);
		if _length
		{
			var _textModify = "";
			var _i = 1, _char, _charControl;
			do
			{
				_char = string_char_at(_text, _i);
				_charControl = _symbol_control[$ _char];
				
				if(!is_undefined(_charControl))
					_char = _charControl;
				
				_textModify += _char;
			} until (_i++ == _length);
			return _textModify;
		}
		return "";
	}
	
}