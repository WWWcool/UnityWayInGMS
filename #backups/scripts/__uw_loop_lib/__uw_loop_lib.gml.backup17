// 2021-03-27 20:42:30
// Unity way library. For more information see the documentation here:
// https://github.com/WWWcool/UnityWayInGMS/wiki


function UWDelegate(_size) constructor
{
    stack = [];
    limit = (_size == undefined ? infinity : _size);
    bind = undefined;
    
    static Bind = function(_instance_or_struct) 
    {
        if(!is_struct(_instance_or_struct))
        {
            if(!instance_exists(_instance_or_struct)) return self;
        }
        bind = _instance_or_struct;
        return self;
    }
    
    static Add = function(_method_or_function, _argument)
    {
        if (array_length(stack) >= limit) return false;
        
        if (is_method(_method_or_function)) 
            _method_or_function = method_get_index(_method_or_function);
        
        var _cell =
        {
            execute: _method_or_function,
            argument: _argument
        }
        
        array_push(stack, _cell);
        return true;
    }
    
    static Run = function() 
    {
        var _size = array_length(stack), _i = -1, _cell;
        while (++_i < _size)
        {
            _cell = stack[_i];
            _cell.execute(_cell.argument);
        }
    }
    
}